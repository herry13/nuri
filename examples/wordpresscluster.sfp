include "modules/hpcloud/hpcloud.sfp"
include "modules/vm/vm.sfp"
include "modules/mysql/mysql.sfp"
include "modules/wordpresscluster/wordpresscluster.sfp"
include "modules/apache/apache.sfp"
// cloud proxy node
proxy isa Node {
	sfpAddress is "localhost"
	cloud isa HPCloud {
		vm_image is "af1f026e-750b-496e-aae4-1635941a472a"
		vm_ssh_key_name is "default"
	}
}
// VM for database
vmdb isa VM {
	in_cloud is proxy.cloud
	final associate_public_ip is true
	mysql isa Mysql {
		running is true
	}
	wp_db isa WordpressDB {
		installed is true
		mysql is vmdb.mysql
	}
}
// VMs for application layer
vmapp1 isa VM {
	in_cloud is proxy.cloud
	final associate_public_ip is true
	apache isa Apache {
		running is true
		php_module is true
		php_mysql_module is true
		server_name is "foo.com"
	}
	wp_web isa WordpressWeb {
		http is vmapp1.apache
		database is vmdb.wp_db
	}
}
vmapp2 extends vmapp1
!vmapp2.wp_web.http is vmapp2.apache
vmapp3 extends vmapp1
!vmapp3.wp_web.http is vmapp3.apache
// VM for load balancer
vmlb isa VM {
	in_cloud is proxy.cloud
	final associate_public_ip is true
	apachelb isa Apache {
		running is true
		lb_members is [vmapp1, vmapp2, vmapp3]
	}
}
// the global constraints
global {
	if vmlb.apachelb.running then {
		vmapp1.apache.running
		vmapp2.apache.running
		vmapp3.apache.running
	}
	if vmapp1.apache.running then vmdb.mysql.running
	if vmapp2.apache.running then vmdb.mysql.running
	if vmapp3.apache.running then vmdb.mysql.running
}

