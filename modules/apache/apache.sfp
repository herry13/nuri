include "modules/service/service.sfp"

schema Apache extends Service {
	port = 80
	document_root = "/var/www"
	php_module = false
	php_mysql_module = false
	server_name = ""

	procedure set_document_root (target isref String) {
		condition {
			this.running = false
			this.installed = true
		}
		effect {
			this.document_root = target
		}
	}

	procedure set_server_name (target isref String) {
		condition {
			this.running = false
			this.installed = true
		}
		effect {
			this.server_name = target
		}
	}

	procedure uninstall {
		condition {
			this.running = false
			this.php_module = false
			this.php_mysql_module = false
		}
		effect {
			this.installed = false
		}
	}

	procedure install_php_module {
		condition {
			this.installed = true
			this.running = false
		}
		effect {
			this.php_module = true
		}
	}

	procedure uninstall_php_module {
		condition {
			this.running = false
		}
		effect {
			this.php_module = false
		}
	}

	procedure install_php_mysql_module {
		condition {
			this.php_module = true
			this.installed = true
			this.running = false
		}
		effect {
			this.php_mysql_module = true
		}
	}

	procedure uninstall_php_mysql_module {
		condition {
			this.running = false
		}
		effect {
			this.php_mysql_module = false
		}
	}
}
