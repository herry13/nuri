include "../apache/apache.sfp"
include "../mysql/mysql.sfp"

schema WordpressCluster

schema WordpressDB extends WordpressCluster {
	installed : Bool
	mysql isref Mysql
	final db_name = "wordpress"
	final db_user = "wordpress"
	final db_password = "password"

	sub install(mysql: Mysql) {
		condition {
			this.installed != true
			mysql.running = true
			mysql.public = true
		}
		effect {
			this.installed = true
			this.mysql = mysql
		}
	}
	sub uninstall (mysql: Mysql) {
		condition {
			this.mysql = mysql
			mysql.running = true
			this.installed = true
		}
		effect {
			this.installed = false
		}
	}
}

schema WordpressWeb extends WordpressCluster {
	installed : Bool
	http isref Apache
	database isref WordpressDB
	final path = "/var/www/wp"
	final source_url = "http://wordpress.org/latest.tar.gz"

	sub install (http: Apache, database: WordpressDB) {
		condition {
			this.installed != true
			http.installed = true
			http.running = false 
			http.php_module = true
			http.php_mysql_module = true
			database.installed = true
		}
		effect {
			this.installed = true
			this.http = http
			this.database = database
		}
	}

	sub uninstall (http: Apache, database: WordpressDB) {
		condition {
			this.installed = true
			this.http = http
			http.running = false
			http.installed = true
			this.database = database
			database.installed = true
		}
		effect {
			this.installed = false
			this.http = null
			this.database = null
		}
	}
}
