include "modules/apache/apache.sfp"

class Apachelb extends Service {
	port = 80
	members isset Machine
	server_name = ""

	procedure set_members (members isset Machine) {
		condition {
			this.installed = true
			this.running = false
			foreach (members as m) {
				m.running = true
			}
		}
		effect {
			this.members = members
		}
	}

	procedure set_server_name (name isref String) {
		condition {
			this.installed = true
			this.running = false
		}
		effect {
			this.server_name = name
		}
	}

	procedure install {
		condition {	}
		effect {
			this.installed = true
		}
	}

	procedure uninstall {
		condition {
			this.running = false
		}
		effect {
			this.installed = false
		}
	}

	procedure start {
		condition {
			this.installed = true
		}
		effect {
			this.running = true
		}
	}

	procedure stop {
		condition { }
		effect {
			this.running = false
		}
	}
}
